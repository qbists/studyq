# 2. Even Fibonacci numbers

## Problem

Each new term in the Fibonacci sequence is generated by adding the previous two terms. By starting with 1 and 2, the first 10 terms will be:

1, 2, 3, 5, 8, 13, 21, 34, 55, 89, ...

By considering the terms in the Fibonacci sequence whose values do not exceed four million, find the sum of the even-valued terms.


## Solution 1

On [community.kx.com](https://community.kx.com/t5/kdb-and-q/Q-For-Problems-Episode-2/td-p/13148) jkane17 offers

```q
q)fib:{x,sum -2#x}/[;1 2]
q)fib 10
1 2 3 5 8 13 21 34 55 89 144 233
```

The definition of `fib` exploits the rule that the Over iterator applied to a function `f` derives a variadic function `f/`. Projecting that on `[;0 1]` ensures the binary form of `f/` is projected. The resulting unary will be the [Do](https://code.kx.com/q/ref/accumulators/#do) or [While](https://code.kx.com/q/ref/accumulators/#while) form of Over, depending on the argument it is applied to. Above, applied to 10, it is the Do form. Applied to a test function, it is the While form:

```q
q)fib {x>last y}[4000000]
1 2 3 5 8 13 21 34 55 89 144 233 377 610 987 1597 2584 4181 6765 10946 17711 ..
```
This ends with the first Fibonacci number to exceed 4,000,000.

```q
q)-5#fib {x>last y}[4000000]
832040 1346269 2178309 3524578 5702887
```

So we need to `-1_` to drop the last term.

We can find the even numbers with `mod 2`.

```q
q)sum{x where not x mod 2} -1 _ fib {x>last y}[4000000]
4613732
```


## Solution 2

The projection used as a truth function could be written as a composition:

```q
q)sum{x where not x mod 2} -1 _ fib (4000000>last@)
4613732
```

And the arithmetic of `mod` can be replaced by selecting every third item.

```q
q)sum{x where count[x]#001b}0, -1 _ fib (4000000>last@)
4613732
```

But with only 34 Fibonacci numbers under 4 million, there is no significant advantage.

