# 6. Sum square difference

https://projecteuler.net/problem=6

> The sum of the squares of the first ten natural numbers is 385.
> The square of the sum of the first ten natural numbers is 3025.
> Hence the difference between the sum of the squares of the first ten natural numbers and the square of the sum is 
>	3025 − 385 = 2640 .
>
> Find the difference between the sum of the squares of the first one hundred natural numbers and the square of the sum.


## Solutions

This is close to trivial in q.

```q
nnt:1+til@  / natural numbers to x
sqr:{x*x}
{(sqr sum x)-sum sqr x} nnt 100
```

## Alternatives

We can explore some alternatives. 

The square of the sums, the sum of the squares – two compositions?

```q
(-).(sqr sum@;sum sqr@)@\:nnt 100
```

We can factor out the repetitions. 
The Zen monks will give us `sqr` and `sum` both reversed and not; `1 reverse\(sqr;sum)`, a 2×2 matrix.

```q
q)1 reverse\(sqr;sum)
{x*x} sum
sum   {x*x}
```

[Compose](https://code.kx.com/q/ref/compose) `'[;]` is binary (composes two functions) so can be applied by Apply Each Right `./:` to each row of the matrix to produce the two compositions.

```q
q)'[;]./:1 reverse\(sqr;sum)
{x*x}sum
sum{x*x}
```

It remains only to Apply At Each Left `@\:` the compositions to the vector and take the difference.

```q
.[-] ('[;] ./: 1 reverse\(sqr;sum) ) @\: nnt 100
```

Composition rocks – and opens the way for cases where the functions (here `sqr` and `sum`) are determined at runtime. 

Improving readability here… not so much.
<br>

### Constant time solution

The sum of first n natural numbers is given by

$$    
    S_n = n * ( n + 1 ) / 2
$$
<br>

```q
// Sum of the first x positive integers
nsum:{"j"$.5*x*x+1}
```
<br>

The sum of the first n squares is given by

$$
    S_{n^2} = n * ( 2n + 1 )( n + 1 ) / 6
$$
<br>

```q
// Sum of the squares of the first x positive integers
n2sum:{div[x*(1+2*x)*x+1;6]} 
```
<br>

```q
{(prd 2#nsum x)-n2sum x} 100
```

---

Contributors

* Stephen Taylor
* Alex Unterrainer
* Jonathan Kane
