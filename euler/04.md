# 4. Largest palindromic product

## Problem

https://projecteuler.net/problem=4

A palindromic number reads the same both ways. 
The largest palindrome made from the product of two 2-digit numbers is 9009=91×99.
Find the largest palindrome made from the product of two 3-digit numbers.


## Solution 1

Our candidates are the products of the unique pairs of 3-digit numbers:

```q
c:prd each distinct asc each {x cross x} 100+ til 1000-100  / candidate products
ip:{x~reverse x}                                            / is palindrome?
max c where ip each string c
```


## Optimisation

### Test

The palindrome test is famous in k (`{x~|x}`) and only slightly longer in q: `{x~reverse x}`.

Jonathan Kane [proposes](https://community.kx.com/t5/kdb-and-q/Q-For-Problems-Episode-4/td-p/13254) an optimisation for this case: reverse the string and Tok to long, then compare the original number – Equals on longs is faster than Match on strings.
But is the time saved more than the Tok takes? 

```q
q)/ atom
q)\ts:10000 {x~reverse x} string 905509      / classic
4 1280
q)\ts:10000 {x="J"$reverse string x} 905509  / variant
4 1488

q)/ vector
q)p:10000?1000000
q)\ts:100 {x~reverse x}each string p         / classic
229 1049152
q)\ts:100 p="J"$reverse each string p        / variant
147 1049200
```

Yes! No interesting difference testing an atom, but the variant is faster with a vector.
The difference is probably shared between Equals on longs being faster than Match on strings, and `string` being faster than `string each`.


### Search

But there is no need to test every candidate. 
When we have found the highest palindrome we can stop.
We can loop through the products in descending order:

```q
i:count C:asc c
while[not ip string C i-:1;]
C i
```

The cost of the sort and the explicit loop is repaid by not having to cast the entire list to strings and test each one. 

Testing atoms, we stay with the classic test. 

```q
q)ipv:{x~'"J"$reverse each string x}  / vector test
q)\ts max c where ipv c
107 32303200
q)\ts i:count C:asc c; while[not ip string C i-:1;]; C i
10 12584304
```

A functional version of the above:

```q
.[@] ({not ip string x y}.) @[;1;-1+]/ (asc c;count[c]-1)
```

Note the use of `@[;1;-1+]/` to decrement the index, and `.[@]` to resolve the result.


## Generalisation

From this we can see a general form for finding the last item that passes a test:

```q
lastitem:{[test;list].[@] ({x y z}[test].) @[;1;-1+]/ (list;-1+count list)}
lastitem[not ip string@] asc c
```

## Solution 2

We can try approaching the problem in reverse to solution 1. Instead of finding the products and checking if they are palindromes, we could try creating all possible palindromes and check if they have products.


### Create palindromes

We can create all possible palindromes by starting in the centre and building out.

```q
centre:string til 10;

"0",/:centre,\:"0"
"1",/:centre,\:"1"
...
"9",/:centre,\:"9"

// setting the results from above as the new centre, we can run the process again to build out our palindromes
"0"      "000"      "00000"
"1"      "010"      "00100"
...  ->   ...   ->    ...
"8"      "989"      "99899"
"9"      "999"      "99999"

```

If we start with `reverse string til 10` then the palindromes we create will be made in descending order

```q
// Create all palindromes in reverse order
digits:reverse string til 10;
palindromes:{[x;y;z]raze x,/:'y,\:/:x}[digits]/[;til x-1];  // x is the count of digits 
pals:"J"$palindromes 2#/:digits;
```

### Check for products

We can create all x-digit numbers using the following code 

```q
// Where x = count of digits in number
nums:reverse r[1]+til(-/)r:`long$10 xexp 0 -1+x;
```

To check if a palidrome is valid, we first check if it is a multiple of any of the above numbers.
Then by dividing this number by the palindrome we find the other product.
  If this number is x-digits in size, then it is a palindrome
  
Because the palindromes are in descending order, we can early exit as soon as first valid palindrome is found.

```q
// Recursively check each palindrome, early exit if found
{[pals;nums]
    p:first pals;
    b:and[first[nums]>n]not mod[;1]n:p%nums mod[p;nums]?0;
    $[b;p;.z.s[1_pals;nums]]
}[pals;nums]
```

### Performance 

This method requires far less memory since we would only need to check against all 3 digit numbers, rather than the the matrix of possible products they could produce.

This combined with the early exit results in much better performance.


```q
\ts f 3 / 906609 -       0      820208
\ts f 4 / 99000099 -     6      13649968
\ts f 5 / 9966006699 -   204    360770864
\ts f 6 / 999000000999 - 14273  8422339312
```

---

Contributors

* Jonathan Kane
* Stephen Taylor
* Alexander Livingston

See also [Q for Problems – Episode 4](https://community.kx.com/t5/kdb-and-q/Q-For-Problems-Episode-4/td-p/13254)
