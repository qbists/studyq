# 53. Maximum Subarray

https://leetcode.com/problems/maximum-subarray/

> Given an integer array `nums`, find the subarray which has the largest sum and return its sum.

The performance of q solutions to the [Maximum Subarray problem](https://en.wikipedia.org/wiki/Maximum_subarray_problem "Wikipedia") appeared on StackOverflow recently.

[In q/kdb+, can I speed up Kadane's algorithm for the maximum subarray problem?](https://stackoverflow.com/questions/74508773/ "StackOverflow")

## Brute force

A  brute-force solution is unlikely to be efficient but will be useful for testing faster candidates. 

Ours generates indices for all the subarrays, calculates the corresponding sums and picks the largest.

```q
q)x:-2 1 -3 4 -1 2 1 -5 4
q)1+tcx:til count x
1 2 3 4 5 6 7 8 9
q)tcx+neg[tcx]_\:til each 1+tcx:til count x
(,0;0 1;0 1 2;0 1 2 3;0 1 2 3 4;0 1 2 3 4 5;0 1 2 3 4 5 6;0 1 2 3 4 5 6 7;0 1 2 3 4 5 6 7 8)
(,1;1 2;1 2 3;1 2 3 4;1 2 3 4 5;1 2 3 4 5 6;1 2 3 4 5 6 7;1 2 3 4 5 6 7 8)
(,2;2 3;2 3 4;2 3 4 5;2 3 4 5 6;2 3 4 5 6 7;2 3 4 5 6 7 8)
(,3;3 4;3 4 5;3 4 5 6;3 4 5 6 7;3 4 5 6 7 8)
(,4;4 5;4 5 6;4 5 6 7;4 5 6 7 8)
(,5;5 6;5 6 7;5 6 7 8)
(,6;6 7;6 7 8)
(,7;7 8)
,,8

q)raze x tcx+neg[tcx]_\:til each 1+tcx:til count x
,-2
-2 1
-2 1 -3
-2 1 -3 4
-2 1 -3 4 -1
-2 1 -3 4 -1 2
-2 1 -3 4 -1 2 1
-2 1 -3 4 -1 2 1 -5
-2 1 -3 4 -1 2 1 -5 4
,1
1 -3
1 -3 4
1 -3 4 -1
1 -3 4 -1 2
1 -3 4 -1 2 1
1 -3 4 -1 2 1 -5
1 -3 4 -1 2 1 -5 4
,-3
-3 4
-3 4 -1
-3 4 -1 2
-3 4 -1 2 1
..

q)mcs0:{max sum each raze x tcx+neg[tcx]_\:til each 1+tcx:til count x}
q)mcs0 x
6
```

## Kadane’s algorithm 

The [OP](https://stackoverflow.com/users/5836951/gabi) on StackOverflow offered `((|[0])(+)::)\[0f;x]` as an implementation of Kadane’s algorithm and wondered if it could run as fast on a 9M list as Python+numba does. For this he would need at least a 4× improvement. 

Your eye will immediately be drawn to the `0f`. What’s a float doing in this? 
And you‘re right. The expression runs 2× faster with the seed set to integer 0; and fractionally faster in a simpler form.

```q
q)M9: -10+9000000?20
q)\ts max((|[0]) (+)::)\[0f;M9]
4367 412436160
q)\ts max((|[0]) (+)::)\[0;M9]
2425 412436160
q)\ts max 0(0|+)\M9
2181 412436096

q)mcs1: max ((0|+)\) @  / Kadane
```

Pause here to notice the very terse composition of `0|` and `+`, equivalent to but slightly faster than the lambda `{0|x+y}`.

```q
q)\ts 0(0|+)\M9
2316 412436000
q)\ts 0{0|x+y}\M9
2051 412436096
```

This implementation of Kadane fails on an edge case: when all the numbers are negative. 
Like a sundial that “marks only the sunny hours”, anytime the max-so-far falls below zero, it gets set to 0. 
If no number is positive the result is wrongly given as zero.

```q
q)0(0|+)\neg 1 + til 5
0 0 0 0 0
```

A more exact implementation of Kadane would track not only the max-since-last-zero, but also the max-seen-so-far in case all the numbers are negative. 
But of course that is nothing but the `max` of the numbers. 
If the expression above returns all 0s, then there are no positive numbers, and we can safely return the maximum instead. 

```q
q)mcs1a:{$[r:max 0(0|+)\x;r;max x]} 
q)mcs1a each(M9;neg 1+til 5)
354 -1
```

This will do nicely if all-negative is a rare edge case. But what if it were often so?

```q
q)M9n:neg 1+abs M9
q)\ts mcs1a M9
2224 412435728
q)\ts mcs1a M9n
2561 412435728
```

We could handle this by modifying the scan to track both the max-since-last zero and also the max-number-seen. At the end, if the latter is negative, that’s the result we want. 

```q
q)mcs2:{{(y;z)z<0}.(0,x 0 0) {(0|y+x 0;x[1]|0|y+x 0; y|x 2)}/x}
q)\ts mcs2 M9
7793 592
q)\ts mcs2 M9n
7865 592
```

Now the two arguments require the same time and space, but execution takes 3× more time than `mcs1a` over 600× less space.


## Vector primitives and overcomputing

The implicit iteration of the vector primitives is so fast that it is often more efficient to ‘overcompute’ than to parse and execute an explicit iteration.
Consider the simple case of determining the range of a vector.

```q
q)\ts:10 (min M9;max M9)
83 960
q)\ts:10 (min;max)@\:M9
82 1024
q)\ts:10 0 0{(y&x 0;y|x 1)}/M9
44158 2192
```

## Vector vector vector

Finally we abandon Kadan’s algorithm for a vector solution from [Nathan Swann](https://stackoverflow.com/users/20498489/nathanswann-aquaq)

```q
q)mcs3:{max s-mins 0^prev s:sums x}
q)mcs3 neg 1+til 5  / handles edge case
-1
q)\ts mcs1 M9
2209 412435616
q)\ts mcs3 M9
84 536871104
```

Here all the iteration is implicit and we see a 50× speed-up from the OP’s solution, well more than the 4× improvement needed to beat Python+Numba.


## Conclusion

