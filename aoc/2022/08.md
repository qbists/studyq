# Day 8: Treetop Tree House 

> The expedition comes across a peculiar patch of tall trees all planted carefully in a grid. The Elves explain that a previous expedition planted these trees as a reforestation effort. Now, they're curious if this would be a good location for a tree house.

Advent of Code 2022 [Day 8](https://adventofcode.com/2022/day/8)


## Ingestion

```q
inp: read0 `:test/08.txt  
```

## Part 1

There are four views of the copse to consider, one from each point of the compass. 

If we write a function `vm` that returns the visibility matrix (boolean) of the copse from one direction, and a function `c4` that applies it from all four compass points, then we can `max` the four matrices to flag the visible trees.

Visibility from the West is `(differ maxs::)'`.

```q
q)inp
"30373"
"25512"
"65332"
"33549"
"35390"

q)(differ maxs::)'[inp]
10010b
11000b
10000b
10101b
11010b
```

Notice that there is no need to cast the char data to integers; chars are sortable.

```q
q)max "ab"
"b"
```

While `rotate` rotates items of a list, `flip reverse::` rotates a matrix through 90° clockwise; while `reverse flip::` is its inverse: 90° anticlockwise.

```q
rot: flip reverse::                              /rotate 90° clockwise
tor: reverse flip::                              /rotate 90° anticlockwise
c4: {[f;m] til[4]{[f;n;m]n tor/f@t}[f]'3 rot\m}  /apply f to m from 4 directions
```

Function `c4` applies its left argument `f` to each of four rotations of matrix `m`, then reverse the various rotations.

```q
q)c4[(differ maxs::)'] inp
10010b 11000b 10000b 10101b 11010b
00000b 00000b 10000b 00101b 11111b
00011b 00101b 11011b 00001b 00011b
11111b 01100b 10000b 00001b 00010b

q)max c4[(differ maxs::)'] inp
11111b
11101b
11011b
10101b
11111b
```

Count the visible trees with either `sum raze` or `2 sum/`.


## Part 2

The rightward viewing distance of the first tree in a row is determined by how many shorter trees are on its right.

Let’s consider three test cases.

```q
q)("30373";"65332";"35251")
"30373"
"65332"
"35251"
q){x[0]>1_x} each ("30373";"65332";"35251")
1000b
1111b
0101b
```

Good: the top 3 can see the adjacent 0; the 6 can see everything to its right; the bottom 3 cannot see the adjacent 5. But the bottom-right 1 is blocked and is *not* visible. Easily fixed.

```q
q){mins x[0]>1_x} each ("30373";"65332";"35251")
1000b
1111b
0000b
```

Summing these is *almost* right for the viewing distance, but we need to add 1 where the view is blocked. 

```q
vd0: {.[+](sum;0b in)@\:mins x[0]>1_x}     /viewing distance of x[0]
q)vd0 each ("30373";"65332";"35251")
2 4 1i
```

Now we can iterate this over the row:

```q
q)(1_)\["30373"]
"30373"
"0373"
"373"
"73"
,"3"
""
```

Ignoring the last 3 items, we append `1 0`, constant for the last two trees in a row.
Applying Each to it gives us the eastward viewing distances for the copse.

```q
q)vd: {,[;1 0i]vd0 each -3_(1_)\[x]}'         /eastward viewing distance 
q)vd inp
2 1 1 1 0
1 1 2 1 0
4 3 1 1 0
1 1 2 1 0
1 2 1 1 0
```

It remains only to use `c4` to apply `vd` from all four directions, and to multiply the resulst together.

```q
q)prd c4[vd] inp
0 0 0 0 0
0 1 4 1 0
0 6 1 2 0
0 1 8 3 0
0 0 0 0 0
```

## Solution

```q
rot: flip reverse::                        /rotate 90° clockwise
tor: reverse flip::                        /rotate 90° anticlockwise
c4: {til[4]{[f;n;t]n tor/f@t}[x]'3 rot\y}  /apply ƒx to y from 4 directions
/part 1
vw: (differ maxs::)'                       /visibility from West
2 sum/max c4[vw] inp
/part 2
vd0: {.[+](sum;0b in)@\:mins x[0]>1_x}     /viewing distance of x[0]
vd: {,[;1 0i]vd0 each -3_(1_)\[x]}'        /eastward viewing distance 
2 max/prd c4[vd] inp
```

## Remarks

The iterators in q are higher-order functions. An iterator takes a function argument**†** and derives a new function that applies it differently. For example, `f'` applies `f` to each item in a list rather than to the whole list.

In q’s ancestor languages J and APL, such higher-order functions are known as (respectively) *adverbs* and *operators*. (Iverson derived this usage from Heaviside, and called + and - simply *functions*; this usage persists in APL.) 

APL and J encourage you to think in terms of higher-order functions. Both languages have primitives that apply a function `f` ‘under’ a function `g` so the result is `gg f g x` where `x` is the argument to `f` and `gg` is the inverse of `g`. 

Q does not have the adverbial riches of J and APL, but its functions are first-class objects, so you can easily roll your own, as in `c4`. 

**†** Iterators can also be applied to lists and dictionaries that represent finite-state machines; i.e. where the values are also keys. 

## Contributors

* Cillian Reilly
* Stephen Taylor

