# Day 8: Treetop Tree House 

> The expedition comes across a peculiar patch of tall trees all planted carefully in a grid. The Elves explain that a previous expedition planted these trees as a reforestation effort. Now, they're curious if this would be a good location for a tree house.

Advent of Code 2022 [Day 8](https://adventofcode.com/2022/day/8)


## Ingestion

```q
inp: read0 `:test/08.txt  
```

## Part 1

There are four views of the copse to consider, one from each point of the compass. 

If we write a function `vm` that returns the visibility matrix (boolean) of the copse from one direction, and a function `c4` that applies it from all four compass points, then we can `max` the four matrices to flag the visible trees.

### Visibility

Visibility from the North is `not(=':)maxs::`.

```q
q)inp
"30373"
"25512"
"65332"
"33549"
"35390"

q)maxs inp
"30373"
"35573"
"65573"
"65579"
"65599"

q)(=':)maxs inp
00000b
10011b
01111b
11110b
11101b

q)not (=':)maxs inp
11111b
01100b
10000b
00001b
00010b
```

Notice that there is no need to cast the char data to integers; chars are sortable.

```q
q)max "ab"
"b"
```

### Four directions

While `rotate` rotates items of a list, `flip reverse::` rotates a matrix through 90° clockwise; while `reverse flip::` is its inverse: 90° anticlockwise.

```q
rot: flip reverse::                              /rotate 90° clockwise
tor: reverse flip::                              /rotate 90° anticlockwise
c4: {[f;m] til[4]{[f;n;m]n tor/f@t}[f]'3 rot\m}  /apply f to m from 4 directions
```

Function `c4` applies its left argument `f` to each of four rotations of matrix `m`, then reverse the various rotations.

```q
q)c4[vn] inp
10010b 11000b 10000b 10101b 11010b
00000b 00000b 10000b 00101b 11111b
00011b 00101b 11011b 00001b 00011b
11111b 01100b 10000b 00001b 00010b

q)max c4[vn] inp
11111b
11101b
11011b
10101b
11111b
```

Count the visible trees with either `sum raze` or `2 sum/`.


## Part 2

The rightward viewing distance of the first tree in a row is determined by how many shorter trees are on its right.

```q
q){x[0]>x}"30373"
01000b
q){mins 1_x[0]>x}"30373"
1000b
q)1+{sum mins 1_x[0]>x}"30373"
2
```

Successively shorten each row.

```q
q)-1_'(1_)\'[inp]
"30373" "0373" "373" "73" ,"3"
"25512" "5512" "512" "12" ,"2"
"65332" "5332" "332" "32" ,"2"
"33549" "3549" "549" "49" ,"9"
"35390" "5390" "390" "90" ,"0"
```
 
Compare to the corresponding trees.

 ```q
 q)inp>-1_'(1_)\'[inp]
01000b 0000b 000b 01b ,0b
00010b 0011b 011b 00b ,0b
01111b 0111b 001b 01b ,0b
00000b 0000b 010b 00b ,0b
00001b 0101b 001b 01b ,0b
```

Calculate the viewing distances.

```q
q)((1+sum mins 1_)'') inp>-1_'(1_)\'[inp]
2 1 1 2 1
1 1 3 1 1
5 4 1 2 1
1 1 2 1 1
1 2 1 2 1
```

But one cannot see out of the copse, so:

```q
q)reverse[til count first inp]&/:((1+sum mins 1_)'') inp>-1_'(1_)\'[inp]
2 1 1 1 0
1 1 2 1 0
4 3 1 1 0
1 1 2 1 0
1 2 1 1 0
```

```q
vdw: {reverse[til count first x]&/:
  1+((sum mins 1_)'')x>-1_'(1_)\'[x]}      /viewing distance from W
```

It remains only to use `c4` to apply `vdw` from all four directions, and to multiply the results together.

```q
q)prd c4[vdw] inp
0 0 0 0 0
0 1 4 1 0
0 6 1 2 0
0 1 8 3 0
0 0 0 0 0
```

## Solution

```q
rot: flip reverse::                        /rotate 90° clockwise
tor: reverse flip::                        /rotate 90° anticlockwise
c4: {til[4]{[f;n;t]n tor/f@t}[x]'3 rot\y}  /apply ƒx to y from 4 directions
vn: not(=':)maxs::                         /visibility from N
2 sum/max c4[vn] inp                       /part 1
vdw: {reverse[til count first x]&/:
  1+((sum mins 1_)'')x>-1_'(1_)\'[x]}      /viewing distance from W
2 max/prd c4[vdw] inp                      /part 2
```

## Remarks

The iterators in q are higher-order functions. An iterator takes a function argument<sup>[1]</sup>sup> and derives a new function that applies it differently. For example, `f'` applies `f` to each item in a list rather than to the whole list.

Long before Y-combinators, q’s ancestor languages knew such higher-order functions as *adverbs* in J and *operators* in APL. (Iverson copied this usage from Heaviside, and called + and - simply *functions*; the usage persists in APL.) 

APL and J encourage you to think in terms of higher-order functions. For example, both languages have primitives that apply a function `f` ‘under’ a function `g` and its inverse `gi` so the result is `gi f g x` where `x` is the argument to `f`. 

Q does not have the adverbial riches of J and APL, but its functions are first-class objects, so you can roll your own, as in `c4`. 

[1] Iterators can also be applied to lists and dictionaries that represent finite-state machines; i.e. where the values are also keys. 

## Contributors

* Cillian Reilly
* Stephen Taylor

