# Day 1: Calorie Counting


> Santa’s reindeer typically eat regular reindeer food, but they need a lot of magical energy to deliver presents on Christmas. For that, their favorite snack is a special type of star fruit that only grows deep in the jungle. The Elves have brought you on their annual expedition to the grove where the fruit grows.

Advent of Code 2022 [Day 1](https://adventofcode.com/2022/day/1)

## Ingestion

```q
inp: read0`:input/01.txt
loads: sum each{(0,where null x)_ x}"I"$inp
```

Above, `read0` returns a list of strings that [Tok](https://code.kx.com/q/ref/tok) interprets as integers. 
Note that Tok iterates implicitly through the strings; no Each is needed. 

Cillian found a way to avoid the `sum`.

```q
value each"++"vs"+"sv inp
```

This is cunning. He uses `"+"` to join the strings, which leaves `"++"` separating the records, which he can then evaluate with `value each`.

```q
q)"+"sv read0 `:test/01.txt
"1000+2000+3000++4000++5000+6000++7000+8000+9000++10000"
q)"++"vs"+"sv read0 `:test/01.txt
"1000+2000+3000"
"4000"
"5000+6000"
"7000+8000+9000"
"10000"
```

Nick Psaris read the input in binary and cast to character

```q
q)"\n\n"vs"c"$read1 `:test/01.txt
"1000\n2000\n3000"
"4000"
"5000\n6000"
"7000\n8000\n9000"
"10000"
```

and used `get` (a synonym of `value`) to evaluate.

```q
loads:(sum get::) each "\n\n"vs"c"$read1 `:test/01.txt
```

Sean Ang used string search-and-replace to wrangle the input.

```q
q)ssr[;"  ";";sum "]" "sv read0 `:test/01.txt
"1000 2000 3000;sum 4000;sum 5000 6000;sum 7000 8000 9000;sum 10000"
q)value {"(sum ",x,")"} ssr[;"  ";";sum "]" "sv read0 `:test/01.txt
6000 4000 11000 24000 10000
```

### Cillian’s straight vector ingestion

A ‘straight’ vector solution avoids partitioning vectors into lists of lists.
Because q handles vectors efficiently, straight-vector solutions are often very fast.

Cillian found one using sorts and Match Each Prior (`~':`).

```q
q)show s:sums"J"$read0`:test/01.txt
1000 3000 6000 6000 10000 10000 15000 21000 21000 28000 36000 45000 45000 55000
q)(~':)s
00010100100010b
q)s*(~':)s
0 0 0 6000 0 10000 0 0 21000 0 0 0 45000 0
q)desc deltas asc s*(~':)s
24000 11000 6000 4000 0 0 0 0 0 0 0 0 0 0
```

```q
loads:desc deltas asc s*(~':)s:sums"J"$read0`:test/01.txt
```

And we are happy to ignore the trailing zeroes. 

Once the loads have been calculated, the rest is trivial.


## Solutions

```q
max loads         /part 1
sum 3#desc loads  /part 2
```

Or, since we have to sort for Part 2 anyway, we can lose the `max`.


```q
(first;sum 3#::)@\:desc loads
```


## Contributors

* Sean Ang
* Cillian Reilly
* Nick Psaris
* Stephen Taylor
