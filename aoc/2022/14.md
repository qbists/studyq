# Day 14: Regolith Reservoir

> The distress signal leads you to a giant waterfall! Actually, hang on - the signal seems like it's coming from the waterfall itself, and that doesn't make any sense. However, you do notice a little path that leads behind the waterfall.
>
> Correction: the distress signal leads you behind a giant waterfall! There seems to be a large cave system here, and the signal definitely leads further inside.

Advent of Code 2022 [Day 14](https://adventofcode.com/2022/day/14)


## Ingestion

```q
q)show inp: read0 `:test/14.txt
498,4 -> 498,6 -> 496,6
503,4 -> 502,4 -> 502,9 -> 494,9
```


## Strategy

Our strategy is fairly simple. We shall draw a map of the air (0s) and the rock paths (1s), and use it to see where a sand grain eventually falls, then mark that position on the map as fallen sand (2s). The Converge iterator will run `drop` until the map stops changing, i.e. further sand grains are falling through. 
Something like:

```q
Map: { .. } inp                              /mark 1s amid 0s
fall: {[map;xy] .. }                         /return next xy
drop: {[map] .[map;;:;2] fall[map]/[0 500]}  /mark fallen sand
sum raze 2=drop/[Map]
```


## Draw the map

The input is a list of paths, each described as a list of coordinate pairs.
We begin by converting them to a list of start and end pairs.

```q
q)show I: (reverse'')raze{1_(;)prior x}@'(get'') " -> "vs/:inp
6 498 4 498
6 496 6 498
4 502 4 503
9 502 4 502
9 494 9 502

q)first I  /first segmemnt
6 498
4 498
```

Notice above the use of `(;)`. List notation is syntactic sugar for `enlist`, so `(;)` is the projection `enlist[;]` and `(;)prior` returns each element paired with the one that precedes it.

The line segments are either vertical or horizontal.
The first segment above is vertical: only the first item of each varies. 

For a coord pair, return the coords through which the line runs.

```q
range: {$[.[=]x;first x;{x+til y-x-1}. asc x]}  / x => y inclusive
between: .[,'] (range') flip ::
```

Above, `range` returns the inclusive range of its argument; or, if the values match, the value as an atom.

```q
q)range 498 500
498 499 500
q)range 500 500
500
```

This means `range each flip` of a coord pair returns an atom and a list, or a list and an atom. 

```q
q)I 0
6 498
4 498
q)range each flip I 0
4 5 6
498
q)I 1
6 496
6 498
q)range each flip I 1
6
496 497 498
```

Either way, we can apply Join Each to them to get a list of coords.

```q
q)between I 0
4 498
5 498
6 498
q)between I 1
6 496
6 497
6 498
```

This gives us `line`, which draws the segment described by coord pair `pts` on its `map` argument.

```q
line: {[map;pts] ./[map;between pts;:;] 1}  / draw rock line
```

The `map` argument is a matrix of 0s, representing air; `line` marks with 1s the position of a line segment.
(Later we shall mark fallen sand with 2s and falling sand with a 3.)

We’ll make our map a thousand points wide and one row deeper than the deepest rock. 
Any sand grain that reaches this level has fallen through and continues to the abyss.

```q
Map: { /air and rock
  I: (reverse'')raze{1_(;)prior x}@'(get'') " -> "vs/:x;
  ((2 0+0 1000|max raze I)#0)line/I }  inp
```

## Visualisation

That’s a fine map there but it would be comforting to be able to read it.

```q
q)Map
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0..
```

We can replicate the AOC display:

```q
disp: {[map;xy]                           /display map with sand at xy   
  m: .[map;;:;3] xy;                      /  mark sand
  r: (first;last)@\:where any m;          /  range of rock and sand
  lbl: (;500;) . r;                       /  col labels
  c: range r+ -1 1;                       /  display columns
  s: string[til count m],'" ";            /  side labels
  s: {(m+1)$neg[m:max count each x]$x}s;  /    aligned
  w: count s 0;                           /  side width
  h: (w+count c;3)#" ";                   /  header
  h: flip@/[h;w+c?lbl;:;] string lbl;     /    with labels 
  h, s,' ".#o+"m[;c] }[;0 500]
```

This allows us to view the map.

```q
q)disp Map
"     4     5  5 "
"     9     0  0 "
"     4     0  3 "
" 0  .......+...."
" 1  ............"
" 2  ............"
" 3  ............"
" 4  .....#...##."
" 5  .....#...#.."
" 6  ...###...#.."
" 7  .........#.."
" 8  .........#.."
" 9  .#########.."
"10  ............"
```

## All that fall

The map determines how the sand grain at coord `xy` will fall. 

```q
fall: {[map;xy] first p where not map ./:p: xy+/:(1 0;1 -1;1 1;0 0) }
```

We generate `p`, its possible destinations from `xy` in order of preference, and return the first for which the map shows air. 
Now we can watch the sand fall.

```q
q)fall[Map]\[0 500]
0 500
1 500
2 500
3 500
4 500
5 500
6 500
7 500
8 500
```

It falls to 8 500 and can fall no further. 

What happens if we drop the grain from 0 504, where there is nothing to catch it?

```q
q)fall[Map]\[0 504]
0  504
1  504
2  504
3  504
4  504
5  504
6  504
7  504
8  504
9  504
10 504
```

We see it stops on the last row. 

We did not have to test for it reaching the end. The possible destinations in `p` included coords that were off the map, and `map .` returned null values, leaving only the starting position. The Converge iterator stopped when the coords stopped changing. 

We complete the drop by marking the end position with a 2 (fallen sand) – unless it has stopped on the last row, i.e fallen through.

```q
drop: {[map] .[map;p;:;] 2*count[map]>1+first p: fall[map]/[0 500] }
```

## Part 1

It remains only to keep dropping grains until the map stops changing.

```q
q)sum raze 2=drop/[Map]                            /part 1
24i
```

## Part 2

For the second part we need only neglect to turn into air those sand grains that reach the last row

```q
drop2: {[map] .[map;;:;2] fall[map]/[0 500] }
```

and keep dropping grains until sand reaches 0 500.

```q
q)sum raze 2=m:{not x . 0 500} drop2/Map           /part 2
93i
```


## Comments

Our solution avoids writing what could otherwise have been several loops and tests: 

* Each grain’s next destination candidates were generated in preference order with Add Each Right `+/:`, checked on the map with Index Each Right `./:`, and the first to qualify selected.
* The `fall` function does not test whether points are off the map: indexing the map with them produces nulls, which `not` excludes.
* The Converge iterator tracked each grain (`fall[map]/`) until it stopped.
* The Converge iterator kept dropping sand grains (`drop/[Map]`) until the map stopped changing. 


## Contributors

* András Dőtsch
* Stephen Taylor
