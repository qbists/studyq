# Day 11: Monkey in the Middle

> As you finally start making your way upriver, you realize your pack is much lighter than you remember. Just then, one of the items from your pack goes flying overhead. Monkeys are playing Keep Away with your missing things!
>
> To get your stuff back, you need to be able to predict where the monkeys will throw your items. After some careful observation, you realize the monkeys operate based on how worried you are about each item.

Advent of Code 2022 [Day 11](https://adventofcode.com/2022/day/11)


## Ingestion

```q
inp: read0 `:test/11.txt

is: update c:0 from flip `W`o`n`t`f!flip
  {get each(raze 4_;raze("{[old]";;;;"}").-3#;last;last;last)@'" "vs'1_6#x}
  each (0,1+where not count each inp)_inp
```

```q
q)inp
"Monkey 0:"
"  Starting items: 79, 98"
"  Operation: new = old * 19"
"  Test: divisible by 23"
"    If true: throw to monkey 2"
"    If false: throw to monkey 3"
""
"Monkey 1:"
..

q)is  /initial state
W           o              n  t f c
-----------------------------------
79 98       {[old]old*19}  23 2 3 0
54 65 75 74 {[old]old+6}   19 2 0 0
79 60 97    {[old]old*old} 13 1 3 0
74          {[old]old+3}   17 0 1 0
```

In `is` we have the input as a table. Notice that the `o` column is a vector of unary functions, not (say) their character representations. Values in this column can be applied directly, as we shall see. 

Notice also the use of `("{[old]";;;;"}").` to arrange the operation parts.
List notation is syntactic sugar for `enlist`, so the above is equivalent to `enlist["{[old]";;;;"}"].`

We have the complete state as a table.
We can represent a monkey’s turn with a function that returns the changed state.
It will need as an argument the monkey index, so a whole round will be 

```q
is turn/til count is
```

A turn begins with new worry levels. 

```q
q)t: is i:0  /turn dict
q)show w: div[;3] .[@]t`o`W
500 620
```

Distribution: where these items go depends on modulus 23.

```q
q)w mod t`n
17 22
q)not w mod t`n
00b
q)`f`t not w mod t`n
`f`f
q)t `f`t not w mod t`n
3 3
q)show d: w group t `f`t 0=w mod t`n  /distribution
3| 500 620
```

They both go to monkey 3.

It remains only to update the state table.

```q
.[;(i;`W);0#] .[;(key d;`W);{x,y};value d] .[;(i;`c);+;count w] is
```

The first (rightmost) Amend increments the monkey’s count of items considered.
The next Amend appends the new worry levels for those items as shown in `d`, i.e. both to monkey 3.
The last (leftmost) Amend empties the list of items for monkey 0: his turn is done. 

```q
turn: {[s;i]                                  /state; monkey#
  t: s i;                                     /  turn dict
  w: div[;3] .[@]t`o`W;                       /  worry levels
  d: w group t `f`t not w mod t`n;            /  distribution
  .[;(i;`W);0#] .[;(key d;`W);{x,y};value d] .[;(i;`c);+;count w] s }
```

Notice it is clear above that only row `i` of the state `s` is read, and that `s` remains unchanged until the last line returns an amended version of it.

```q
q)round: {x turn/til count x}
q)prd 2#desc@[;`c]20 round/is                 /part 1
10605
```


## Part 2

It looks like Part 2 requires us only to refactor `turn` to make the division optional.

We can give `round` a function argument and pass it to `turn` to apply.

```q
turn: {[f;s;i]                                /fn; state; monkey#
  t: s i;                                     /  turn dict
  w: f .[@]t`o`W;                             /  worry levels
  d: w group t `f`t not w mod t`n;            /  distribution
  .[;(i;`W);0#] .[;(key d;`W);{x,y};value d] .[;(i;`c);+;count w] s }

round: {[f;s] s turn[f]/til count s}
prd 2#desc@[;`c]20 round[div[;3]]/is          /part 1
prd 2#desc@[;`c]20 round[::]/is               /part 2
```

But we are in for a shock. Without division, the worry levels overflow the long datatype. 

Fortunately they need not. If we pass on to the next monkey worry levels that are ``mod[;prd is`n]`` the modulo tests continue to work.

```q
q)prd 2#desc@[;`c]20 round[mod[;prd is`n]]/is  /part 2
10197
```

## Observations

Some q features to admire:

* terse calculation of worry levels: ``f .[@]t`o`W``
* use of `group` to distribute outgoing items
* the state change for a turn is specified as three successive Amends


## Contributors

* Rory Kemp
* Stephen Taylor

