## Day 3: Rucksack Reorganization

> One Elf has the important job of loading all of the rucksacks with supplies for the jungle journey. Unfortunately, that Elf didn't quite follow the packing instructions, and so a few items now need to be rearranged.

Advent of Code 2022 [Day 3](https://adventofcode.com/2022/day/3)

```q
inp: read0`:input/03.txt
```

## Part 1

Each rucksack (line of `inp`) contains an even number of items and a single item that appears in both. 
We cut the list in two `{(2,count[x]div 2)#x}` and apply a binary `{first x where x in y}` to the halves to return the error.

We project the binary and compose it with the unary. 

```q
err: {first x where x in y} . {(2,count[x]div 2)#x} ::  / identify error
```

The item priorities are their indices in this string:

```q
priorities: " ",.Q.a,.Q.A
```

It remains only to Find and sum their priorities.

```q
sum priorities?err each inp
```

## Part 2

Divide the rucksacks into threes.

```q
groups: {_[;x]where count[x]#100b} inp        / group in threes
```

Use `{x where x in y}/` to return the only common item from the three lists.
Prioritise as before.

```q
badge: first ({x where x in y}/) ::           / identify badge
sum priorities?badge each groups
```

## Refactor

First we notice that `x where x in y` is actually `inter` and both parts of the problem use it: Part 1 between the rucksack halves, and Part 2 between the rucksacks in a group. 

So `first@inter/` works on either the two halves of a rucksack, or a group of rucksacks.

Next we see we can replace `(" ",.Q.a,.Q.A)?` with `1+.Q.an?`

We compose a function that iterates over lists of rucksack halves, or groups of rucksacks.

```q
f:sum 1+ .Q.an? (first@inter/)each
```

The Take operator with null arguments will partition the rucksacks either:

```q
2 0N#/: inp  / into halves
0N 3# inp    / into threes
```

The complete solution:

```q
f: sum 1+ .Q.an? (first@inter/)each
f 2 0N#/: inp  / part 1
f 0N 3# inp    / part 2
```

Or for both parts:

```q
sum each 1+.Q.an?(first@inter/)each'(2 0N#/:;0N 3#)@\:inp 
```

## Contributors

* András Dőtsch
* Ajay Rathore
* Cillian Reilly
* Stephen Taylor

