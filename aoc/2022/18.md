# Day 18: Boiling Boulders

> You and the elephants finally reach fresh air. You’ve emerged near the base of a large volcano that seems to be actively erupting! Fortunately, the lava seems to be flowing away from you and toward the ocean.
>
> Bits of lava are still being ejected toward you, so you’re sheltering in the cavern exit a little longer. Outside the cave, you can see the lava landing in a pond and hear it loudly hissing as it solidifies.

Advent of Code 2022 [Day 18](https://adventofcode.com/2022/day/18)

## Ingestion

Parsing the input is straightforward in this problem, since the droplet’s cubes have coordinate strings that form valid Q expressions. We take advantage of this fact and use Q’s `value` function to convert them to vectors. 

```q
cubes: value each read0 `:test/18.txt
```

## Part 1

For part 1 we are tasked with finding the surface area of the droplet. It’s suggested we consider which sides of the droplet’s cubes are connected to other cubes. Unconnected sides are considered exposed, and so form part of the surface area.

A simple approach is to consider each cube in turn, offset its coordinates in each of the 6 side directions, and check whether the result is a droplet cube. If not, the side is part of the surface area. 

So given the directions:

```q
dirs: (1 0 0;-1 0 0;0 1 0;0 -1 0;0 0 1;0 0 -1)
```

we produce the list of boolean vectors:

```q
q){not in[;cubes] x +/:dirs} each cubes
000000b
011111b
101111b
110111b
111011b
111101b
111100b
111110b
111111b
111111b
111111b
111111b
111111b
```

Above, the cubes are considered in turn using `each` and the Each Right map (`/:`) used to offset the cube’s location to provide its neighbours. Subsequently, the `in` operator tests whether the neighbours belong to the cube list, with the output negated to identify unconnected sides.

Since the 6 entries per boolean vector correspond to the 6 surfaces of the corresponding cube, the final result corresponds to the total number of nonzero entries across all vectors in the list. To find this we flatten the list and take the sum. 

```q
q)show sum raze {not in[;cubes] x +/:dirs} each cubes /part 1
64i
```

Note that in q boolean values can be summed as integers.

## Part 2

In part 2 we find that our methodology is not quite up to scratch! It fails to distinguish between interior and exterior surface area, with only the latter being relevant for the cooling rate calculation!

One way to fix the issue is to consider which cube sides are reachable by a particle that can move to each of its 6 adjacent cube positions and which starts exterior to the droplet.

This formulation suggests we locate a point exterior to the droplet and run a breadth first search. When the search encounters a cube belonging to the droplet, we have encountered part of the surface area.

To help with the search, we define:

```q
cubesmin: -1+min cubes
cubesmax: 1+max cubes
inbox: {all (x;y;z){[d;i](d>=cubesmin[i]-1)&d<=cubesmax[i]+1}'(0 1 2)}.
```

Here, `cubesmin` and `cubesmax` allow us to define a bounding box for the droplet, so that given a location, if it is outside of the box, then we need not consider it further for the search. The function `inbox` checks exactly that; it uses the Each map to check that each component of the location is in the valid range. The dot character (`.`) destructures the vector input to allow the function’s defining lambda to use the natural coordinates `x`, `y` and `z`.

With this in hand, we define the initial state of the search:

```q
visited:enlist -1+cubesmin
q0: enlist cubesmin
surfacecnt: 0
```

and a run function:

```q
run: {[q]
  p: first q;
  $[in[;cubes] p;
    {surfacecnt+:1; 1_ y}[p;q];
    in[p;visited];
    1_ q;
    {visited,:enlist x; (1_ y),cbs where {inbox x} each cbs:{x+/:dirs} x}[p;q]]}
```

For state, we maintain a list of visited cubes and a list of those next to be visited. Each time `run` is called, the next cube from the queue is considered and the state updated appropriately. If the cube belongs to the droplet, then we increment the surface count. If not, then provided it hasn’t already been visited, it is marked as visited and its 6 neighbours are added to the queue.

All that’s left is to run the search until our queue is depleted and show the result:

```q
q)(0<count@)run/ q0
q)show surfacecnt /part 2
58
```

The full solution is given by:

```q
cubes: value each read0 `:test/18.txt
dirs: (1 0 0;-1 0 0;0 1 0;0 -1 0;0 0 1;0 0 -1)
show sum raze {not in[;cubes] x +/:dirs} each cubes /part 1
cubesmin: -1+min cubes
cubesmax: 1+max cubes
inbox: {all (x;y;z){[d;i](d>=cubesmin[i]-1)&d<=cubesmax[i]+1}'(0 1 2)}.
visited: enlist -1+cubesmin
q0: enlist cubesmin
surfacecnt: 0
run: {[q]
  p:first q;
  $[in[;cubes] p;
    {surfacecnt+:1; 1_ y}[p;q];
    in[p;visited];
    1_ q;
    {visited,:enlist x; (1_ y),cbs where {inbox x} each cbs:{x+/:dirs} x}[p;q]]}
(0<count@)run/ q0
show surfacecnt /part 2
```

## Faster solution 

The above solution works, but it’s very iterative in nature. The run function is called for every cube that’s visited. Since q is known for its fast vector primitives, it’s natural to ask if we can do better. It turns out we can, as shown by András Dőtsch’s solution:

```q
inp: read0 `:test/18.txt
I: get@'inp
D: (0-1_4#)\[1 0 0]
s: {[I] sum count@'I except/:D+/:\:I} 
s I /part 1
I+:1
U: cross/[til@'2+max I]
f: {[C] distinct C,U inter raze[D+/:\:C] except I}
s U except f/[1 3#0] /part 2
```

As well as being shorter, it is significantly faster (approx 20×), at the expense of a slight increase in memory usage (1.5×). Let’s unpack how it works.  

It begins similarly to the previous solution, with `get@'` taking the place of `value each`. Next, it differs by generating rather than hardcoding the vector of directions via a clever use of the scan iterator:

```q
q)D: (0-1_4#)\[1 0 0]
q)show D
1  0  0
0  0  -1
0  1  0
-1 0  0
0  0  1
0  -1 0
```

To calculate the surface area, a function `s` is defined and applied to the input list of cubes. Looking at the body of `s`, we see it makes use of a combination of the Each Left and Each Right map iterators to offset each cube through its side directions:

```q
D+/:\:I
```

This generates a list of vectors, with the first item corresponding to the input cubes shifted in direction `(1 0 0)`, the second in direction `(0 0 -1)`, the third in direction `(0 1 0)`, etc. Subsequently, the Each Right map combined with `except` generates a list of vectors with each vector containing the input cubes not present in the corresponding shifted input:  

```q
q)I except/:D+/:\:I
(1 2 2;2 1 2;2 3 2;2 2 1;2 2 3;2 2 4;2 2 6;1 2 5;3 2 5;2 1 5;2 3 5)
(1 2 2;3 2 2;2 1 2;2 3 2;2 2 4;2 2 6;1 2 5;3 2 5;2 1 5;2 3 5)
(1 2 2;3 2 2;2 1 2;2 2 1;2 2 3;2 2 4;2 2 6;1 2 5;3 2 5;2 1 5;2 3 5)
(3 2 2;2 1 2;2 3 2;2 2 1;2 2 3;2 2 4;2 2 6;1 2 5;3 2 5;2 1 5;2 3 5)
(1 2 2;3 2 2;2 1 2;2 3 2;2 2 1;2 2 6;1 2 5;3 2 5;2 1 5;2 3 5)
(1 2 2;3 2 2;2 3 2;2 2 1;2 2 3;2 2 4;2 2 6;1 2 5;3 2 5;2 1 5;2 3 5)
```

Let’s consider what the output above represents. Cubes contained in the first vector are cubes that cannot be reached by moving in direction `(1 0 0)` from any other cube in the input scan. Hence, the fact that `(1 2 2)` is present, means that its side pointing along the negative X axis is unconnected, and so part of the surface area. A similar argument applies in the other directions, and so to calculate the full surface area, we simply need to count the number of elements in each vector in the list and sum the result. Doing so gives the solution to part 1: 

```q
q)s: {[I] sum count@'I except/:D+/:\:I} 
q)show s I
64
```

For part 2, we again apply a search starting from an exterior point, but we do so in vector-friendly way. First, similar to the previous solution, a bounding box is defined to constrain the search. Although in this case, rather than define a function to detect if a cube is within the box, we simply generate all valid cubes within the box. The `cross` function combined with the scan iterator (`/`) provide a compact method to do so:

```q
q)I+:1
q)U: cross/[til@'2+max I]
q)show U
0 0 0
0 0 1
0 0 2
0 0 3
0 0 4
0 0 5
0 0 6
0 0 7
0 0 8
0 1 0
...
```

Next, we define the search function:

```q
f: {[C] distinct C,U inter raze[D+/:\:C] except I}
```

To understand `f`, let’s consider its application to a list of cubes exterior to the droplet. We can start with the list containing the cube at `(0 0 0)`, which is conveniently expressed as `1 3#0`. Then, applying `f` once gives: 

```q
q)show f 1 3#0
0 0 0
0 0 1
0 1 0
1 0 0
```

and applying again yields:

```q
q)show f f 1 3#0
0 0 0
0 0 1
0 1 0
1 0 0
0 0 2
0 1 1
0 2 0
1 0 1
1 1 0
2 0 0
```

We see that each application of `f` expands our set by adding all cubes reachable from the current set. So rather than maintaining a queue of elements next to visit, we instead grow our visited set directly, being careful to exclude cubes that are outside of the bounding box or are part of scanned droplet. If we look closely, this is exactly what `f` is doing.

```q
distinct C,U inter raze[D+/:\:C] except I
```

Every element in `C` (equivalent to the set of visited cubes) is offset in each direction, with the result flattened and cubes contained in the droplet scan removed. The result is then intersected with the bounding box to remove those out of bounds and appended to our initial list. Keyword `distinct` removes any duplicates. 

If we continue applying `f`, then eventually the returned list of cubes will stabilise on the set of cubes exterior to the droplet within the bounding box.

```q
f/[1 3#0]
```

Taking the complement in the bounding box provides a scan of the droplet with its interior filled in:

```q
U except f/[1 3#0]
```

Finally, since the droplet scan is now solid, we can apply `s` again to return only the exterior surface area:

```q
s U except f/[1 3#0]
```

One might worry that this solution does more work than is necessary, in that it offsets some cubes multiple times. For example, `(0 0 0)` is offset each time `f` is called. But it turns out that this extra work is more that compensated for by the reduction in iterations on the outer 'loop'. Since the first solution deals with individual cubes, its run function is called approx 57000 times for the full problem input. Whereas for this solution, the outer loop is called only 60 times, with each call operating on a larger vector input using q primitives.

A very nice solution indeed!

## Contributors

* George Berkeley
* András Dőtsch

