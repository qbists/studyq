# Day 18: Boiling Boulders

> You and the elephants finally reach fresh air. You've emerged near the base of a large volcano that seems to be actively erupting! Fortunately, the lava seems to be flowing away from you and toward the ocean.
>
> Bits of lava are still being ejected toward you, so you're sheltering in the cavern exit a little longer. Outside the cave, you can see the lava landing in a pond and hear it loudly hissing as it solidifies.

Advent of Code 2022 [Day 18](https://adventofcode.com/2022/day/18)

## Ingestion

Parsing the input is straightforward in this problem, since the droplet's cubes have coordinate strings that form valid Q expressions. We take advantage of this fact and use Q's `value` function to convert them to vectors. 

```q
cubes:value each read0 `:test/18.txt
```

## Part 1

For part 1 we are tasked with finding the surface area of the droplet. It's suggested we consider which sides of the droplet's cubes are connected to other cubes. Unconnected sides are considered exposed, and so form part of the surface area.

A simple approach is to consider each cube in turn, offset its coordinates in each of the 6 side directions, and check whether the result is a droplet cube. If not, the side is part of the surface area. 

So given the directions:

```q
dirs:(1 0 0;-1 0 0;0 1 0;0 -1 0;0 0 1;0 0 -1)
```

we produce the list of boolean vectors:

```q
q){not in[;cubes] x +/:dirs} each cubes
000000b
011111b
101111b
110111b
111011b
111101b
111100b
111110b
111111b
111111b
111111b
111111b
111111b
```

Above, the cubes are considered in turn using `each` and the Each Right map (`/:`) used to offset the cube's location to provide its neighbours. Subsequently, the `in` operator tests whether the neighbours belong to the cube list, with the output negated to identify unconnected sides.

Since the 6 entries per boolean vector correspond to the 6 surfaces of the corresponding cube, the final result corresponds to the total number of nonzero entries across all vectors in the list. To find this we flatten the list and take the sum. 

```q
q)show sum raze {not in[;cubes] x +/:dirs} each cubes /part 1
64i
```

Note that in Q boolean values can be summed as integers.

## Part 2

In part 2 we find that our methodology is not quite up to scratch! It fails to distinguish between interior and exterior surface area, with only the latter being relevant for the cooling rate calculation!

One way to fix the issue is to consider which cube sides are reachable by a particle that can move to each of its 6 adjacent cube positions and which starts exterior to the droplet.

This formulation suggests we locate a point exterior to the droplet and run a breadth first search. When the search encounters a cube belonging to the droplet, we have encountered part of the surface area.

To help with the search, we define:

```q
cubesmin:-1+min cubes
cubesmax:1+max cubes
inbox:{all (x;y;z){[d;i](d>=cubesmin[i]-1)&d<=cubesmax[i]+1}'(0 1 2)}.
```

Here, `cubesmin` and `cubesmax` allow us to define a bounding box for the droplet, so that given a location, if it is outside of the box, then we need not consider it further for the search. The function `inbox` checks exactly that; it uses the Each map to check that each component of the location is in the valid range. The dot character (`.`) destructures the vector input to allow the function's defining lambda to use the natural coordinates `x`, `y` and `z`.

With this in hand, we define the initial state of the search:

```q
visited:enlist -1+cubesmin
q0:enlist cubesmin
surfacecnt:0
```

and a run function:

```q
run:{[q]
  p:first q;
  $[in[;cubes] p;
    {surfacecnt+:1; 1_ y}[p;q];
    in[p;visited];
    1_ q;
    {visited,:enlist x; (1_ y),cbs where {inbox x} each cbs:{x+/:dirs} x}[p;q]]};
```

For state, we maintain a list of visited cubes and a list of those next to be visited. Each time `run` is called, the next cube from the queue is considered and the state updated appropriately. If the cube belongs to the droplet, then we increment the surface count. If not, then provided it hasn't already been visited, it is marked as visited and its 6 neighbours are added to the queue.

All that's left is to run the search until our queue is depleted and show the result:

```q
q)(0<count@)run/ q0
q)show surface /part 2
58
```

The full solution is given by:

```q
cubes:value each read0 `:test/18.txt
dirs:(1 0 0;-1 0 0;0 1 0;0 -1 0;0 0 1;0 0 -1)
show sum raze {not in[;cubes] x +/:dirs} each cubes /part 1
cubesmin:-1+min cubes
cubesmax:1+max cubes
inbox:{all (x;y;z){[d;i](d>=cubesmin[i]-1)&d<=cubesmax[i]+1}'(0 1 2)}.
visited:enlist -1+cubesmin
q0:enlist cubesmin
surfacecnt:0
run:{[q]
  p:first q;
  $[in[;cubes] p;
    {surfacecnt+:1; 1_ y}[p;q];
    in[p;visited];
    1_ q;
    {visited,:enlist x; (1_ y),cbs where {inbox x} each cbs:{x+/:dirs} x}[p;q]]};
(0<count@)run/ q0
show surfacecnt /part 2
```

## Contributors

* George Berkeley