# Day 15: Beacon Exclusion Zone

> You feel the ground rumble again as the distress signal leads you to a large network of subterranean tunnels. You don't have time to search them all, but you don't need to: your pack contains a set of deployable sensors that you imagine were originally built to locate lost Elves.
>
> The sensors aren't very powerful, but that's okay; your handheld device indicates that you're close enough to the source of the distress signal to use them. You pull the emergency sensor system out of your pack, hit the big button on top, and the sensors zoom off down the tunnels.

Advent of Code 2022 [Day 15](https://adventofcode.com/2022/day/15)


## Ingestion

```q
q)show inp: read0 `:test/15.txt
Sensor at x=2, y=18: closest beacon is at x=-2, y=15
Sensor at x=9, y=16: closest beacon is at x=10, y=16
Sensor at x=13, y=2: closest beacon is at x=15, y=3
Sensor at x=12, y=14: closest beacon is at x=10, y=16
Sensor at x=10, y=20: closest beacon is at x=10, y=16
Sensor at x=14, y=17: closest beacon is at x=10, y=16
..

q)`S`B set'flip(get'')(":"vs')inp except\:"S=,",.Q.a;  / source and beacon pairs
q)show D: sum each abs S-B                             / distances scanned
7 1 3 4 4 5 9 10 3 8 6 5 1 7
```


## Part 1

A simple approach would be to mark with 1s each sensor’s ‘shadow’ (scanned area) on a boolean map, and sum row 10. But this isn’t going to scale well to sixteen trillion points, and anyway seems wasteful when we are concerned only with row 10. 

Instead we consider where each shadow intersects the line. 
For a given line `Y`, a sensor’s shadow intersects it at two points; one if it just touches the line; zero if it doen’t reach it at all. 
It all depends on whether the shadow overlaps the line.

The shadow overlaps at line `Y` are

```q
q)D - abs S[;1]-10
-1 -5 -5 0 -6 -2 6 0 2 4 -4 2 -6 -2
```

Negatives are where sensor shadows don’t reach the line. 
For those that do we find the intersects by adding and subtracting the overlap to/from the sensor’s x-position.

```q
q)-1 1*/:D - abs S[;1]-10
1  -1
5  -5
5  -5
0  0
6  -6
..
```

But not for negative overlaps.

```q
q)show i:{(x+o*\:-1 1)where 0<=o:D-abs y-10} . flip S  / intersects
12 12
2  14
2  2
-2 2
16 24
14 18
```

Some of these ranges overlap. 

```q
q)show i:asc i
-2 2
2  2
2  14
12 12
14 18
16 24
```

And they might not all be contiguous. 
Merge them into contiguous ranges. 

Reduce the sorted list of ranges: if the next range is contiguous with the last, extend the last; otherwise append a new range.

```q
q){$[2>y[0]-last[x] 1;.[x;(count[x]-1;1);|;y 1];x,enlist y]}\[1#i;1_i]
-2 2
-2 14
-2 14
-2 18
-2 24
```

Here, all the ranges are contiguous so we get only a single range in the result.

```q
q)merge: {$[2>y[0]-last[x] 1;.[x;(count[x]-1;1);|;y 1];x,enlist y]}/
q)merge . (1#;1_)@\: i
-2 24
```

The number of positions in a range:

```q
q){1+y-x} . -2 24
27
```

Though above we have a single range in it, we have a *list* of non-contiguous ranges. We must count positions in each range.

```q
q){1+y-x} . flip merge . (1#;1_)@\: i
,27
```

And subtract the number of beacons in the line.

```q
q)sum (distinct B)[;1]=10
1i
```

Putting it together:

```q
inp: read0`:test/15.txt
Y: 10

`S`B set'flip(get'')(":"vs')inp except\:"S=,",.Q.a;  / source/beacon pairs
D: sum each abs S-B                                  / distances scanned
nB: sum (distinct B)[;1]=Y                           / # beacons on line Y

/ merge contiguous ranges
merge: .[{$[2>y[0]-last[x] 1;
	.[x;(count[x]-1;1);|;y 1];
	x,enlist y]}/] (1#;1_)@\:asc ::                  

/ part 1
intersects: {(S[;0]+o*\:-1 1)where 0<=o:D-abs S[;1]-x}
-[;nB] sum{1+y-x}. flip merge intersects Y                   / part 1
```

## Part 2

In Part 2 we can adapt this strategy. For each line in the search space

1.  calculate and merge the intersects for each sensor
2.  where the merged intersects are not `0,LIM` (i.e. the whole line) return the line number and the intersects

From the intersects, return the lacunae (missing positions).

```q
lac: {raze -1 -1 {1+y[1]+til x[0]-y[1]+1}':,[;1 2#LIM+1] x}  / lacunae in ranges
sum each 4000000 1*/:.[,'](lac;-1+)@'
  {x~y 0}[1 2#0,LIM] {(merge flip(0|;LIM&)@'flip intersects x 1;1+x 1)}/(1 2#0,LIM;0)
```

Above, `lac` takes a list of ranges and returns the values within `0,LIM` not included.

For each line, the lambda calculates and merges the intersection ranges.
The search would eventually find all uncovered points in the search space; the While iteration terminates as soon as one is found.


## Comments

It is often faster in q to ‘overcompute’, where the speed of native operations on vectors makes it faster to compute and search a range of results than loop until a hit is found. 

In this case we could compute the intersections for each line and sensor:

```q
I: S[;0]+/:{x*(2*0<=x)#\:\:-1 1}D-/:abs S[;1]-\:/:til LIM    / intersections
```

When considering an ‘overcompute’ strategy, you have to assess the probability of hittig a result early in the search.


## Optimised

A very much faster solution starts from 

> the fact that any beacon will be adjacent to the perimeter of the region, so there are 4 straight lines that it could possibly pass through. Generate all the equations, and then the beacon will be at one of the intersections.

```q
`Sx`Sy`Bx`By set' flip {get @[x;where not x in "-0123456789";:;" "]}each inp;
m: sum abs (Sx-Bx;Sy-By) / manhattan dists
/ part 1
count except[;Bx where By=Y] distinct raze {$[y<0;();x-y-til 1+2*y]}'[Sx;m-abs Sy-Y]
/ part 2
peri: raze 1 1 -1 -1,''(Sy-Sx+m+1; Sy-Sx-m+1; Sy+Sx+m+1; Sy+Sx-m+1)
int: distinct raze peri {r:0-1%(%/)x-y; (r; sum x*r,1)}\:/: peri
sum 4000000 1*floor first int where {all raze(x=floor x;0<=x;x<=LIM;m<sum abs(Sx;Sy)-x)} each int
```



## Contributors

* Rory Kemp
* Stephen Taylor

