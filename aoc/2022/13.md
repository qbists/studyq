# Day 13: Distress Signal

> You climb the hill and again try contacting the Elves. However, you instead receive a signal you weren't expecting: a distress signal.
>
> Your handheld device must still not be working properly; the packets from the distress signal got decoded out of order. You'll need to re-order the list of received packets (your puzzle input) to decode the message.

Advent of Code 2022 [Day 13](https://adventofcode.com/2022/day/13)


## Ingestion

```q
q)show inp: read0 `:test/13.txt
[1,1,3,1,1]
[1,1,5,1,1]

[[1],[2,3,4]]
[[1],4]

[9]
[[8,7,6]]

..
```

The first job is to parse the strings into a list of signal pairs.

> No need to write your own JSON parser if we have one built in. — Péter Györök

```q
q) show I: 2 cut .j.k each inp where count[inp]#110b
1 1 3 1 1                      1 1 5 1 1
,1f 2 3 4f                     ,1f 4f
9                              8 7 6
(4 4f;4f;4f)                   (4 4f;4f;4f;4f)
7 7 7 7f                       7 7 7f
()                             ,3f
```


## Part 1

Compare the signals in each pair, returning the ‘difference’ between each pair.

Return a positive number if the left argument `x` is ‘greater’ than the right argument `y`; a zero if they are equal, otherwise a negative. 

1.  If `x` and `y` are both atoms, return their difference.
2.  If one argument is an empty list, return the length of the other, negated if `y` is longer.
3.  At least one argument is a list and neither is empty. Recurse on the first item of each. If there is a difference, return it; otherwise, drop the first item of each and recurse.

```q
cmp:{
  $[(0>type x)&0>type y;      `long$x-y;
    (0=count x)|0=count y;    count[x]-count y;
    r:cmp[first x;first y];   r;
                              cmp[1_(),x;1_(),y] ] }
```

Notice in the last line how `(),` ensures the recursion is passed two lists.

It remains only to apply `cmp` to each pair of signals and sum the (origin-1) indices of the negatives.


```q
sum 1+where 1>cmp .' I              /part 1
```

## Part 2

A sortng function `qs`: for each list argument with more than one item, we use `cmp` to get the ‘difference’ between its item and the first item

```q
r: x cmp\:first x
```

and return 

```q
qs[x where r<0],(x where r=0),qs x where r>0
```

But we do not need to examine `r` three times; `group signum` will do it for us.

```q
raze(qs;::;qs)@'x(group signum x cmp\:first x)@-1 0 1
```

Moreover, `qs` does not need to know its own name: `.z.s` denotes the currently running function.

```q
qs: {$[2>count x; x;    
  raze(.z.s;::;.z.s)@'x(group signum x cmp\:first x)@-1 0 1
  ]}
```

Prepend lists corresponding to `[[2]]` and `[[6]]` to the razed signals, and sort.

```q
A: .j.k"[[2]]"; B: .j.k"[[6]]"; 
J: qs(A;B),raze I
```

It remains only to find `A` and `B` in `J` and multiply the indices.

```q
prd 1+(where A~/:J;where B~/:J)     /part 2
```

## With combinator

Notice a recurring pattern, in which the same unary function is applied to both `x` and `y`. We can rewrite our solutions using a ‘both’ combinator.

```q
I: 2 cut .j.k each inp where count[inp]#110b

b: {x@/:(y;z)}  / 'both' combinator

cmp:{
  $[all b[type;x;y]<0;      `long$x-y;
    any b[count;x;y]=0;     .[-] b[count;x;y];
    r:cmp . b[first;x;y];   r;
                            cmp . b[1_(),;x;y] ] }

sum 1+where 1>cmp .' I              /part 1

qs: {$[2>count x; x;    
  raze(.z.s;::;.z.s)@'x(group signum x cmp\:first x)@-1 0 1
  ]}

`A`B set'b[.j.k;"[[2]]";"[[6]]"];
J: qs(A;B),raze I
prd 1+b[where ~/:[;J]::; A; B]     /part 2
```


## Comments

Using Find `?` in the last line, i.e. `J?(A;B)` would be a mistake, because Find is [type-specific](https://code.kx.com/q/ref/find/#type-specific).

```q
q)A~/:J
000000000100000000b
q)where A~/:J
,9
q)J?A
,18
q)A in J
,0b
```

Other ways to write the combinator: `{x each(y;z)}` or `{(x y;x z)}`.


## Contributors

* András Dőtsch
* Stephen Taylor


