# Day 2: Rock Paper Scissors

> The Elves begin to set up camp on the beach. To decide whose tent gets to be closest to the snack storage, a giant Rock Paper Scissors tournament is already in progress.

Advent of Code 2022 [Day 2](https://adventofcode.com/2022/day/2)

## Ingestion

The strategy is the first and third columns of the input.

```q
q)show strategy: .[;(::;0 2)] inp:read0 `:test/02.txt
"AY"
"BX"
"CZ"
```

## Solution

We could code the rules of Rock Paper Scissors as a function `ldw` that maps each pair to lose/draw/win. But there are only nine possible pairs. 
A dictionary has fewer moving parts.

```q
q)show ldw:3*cut[2;"BXCYAZAXBYCZCXAYBZ"]!til[3]where 3#3 / lose; draw; win
"BX"| 0
"CY"| 0
"AZ"| 0
"AX"| 3
"BY"| 3
"CZ"| 3
"CX"| 6
"AY"| 6
"BZ"| 6
```

To which we add the score for the response choice.

```q
q)show score:ldw+1+"XYZ"?(key ldw)[;1]
"BX"| 1
"CY"| 2
"AZ"| 3
"AX"| 4
"BY"| 5
"CZ"| 6
"CX"| 7
"AY"| 8
"BZ"| 9

q)score each strategy
8 1 6
```

But now we see that we can replace the dictionary with Find.

```q
q)1+string[`BX`CY`AZ`AX`BY`CZ`CX`AY`BZ]?strategy
8 1 6
```

And we can solve Part 1 with

```q
rounds: string`BX`CY`AZ`AX`BY`CZ`CX`AY`BZ
score: sum 1+ rounds? ::
score strategy
```


## Part 2

Part 2 introduces a preliminary mapping before we can score.

```q
q)show play:rounds!rounds[;0],'"XZYZYXYXZ"
"BX"| "BX"
"CY"| "CZ"
"AZ"| "AY"
"AX"| "AZ"
"BY"| "BY"
"CZ"| "CX"
"CX"| "CY"
"AY"| "AX"
"BZ"| "BZ"

q)score play strategy
12
```

Or, for Parts 1 & 2 together

```q
q)score each 1 play\ strategy
15 12
```

To do: discuss solutions from Andr√°s, Nick, Attila
